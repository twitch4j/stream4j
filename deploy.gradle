
/**
 * Sources - build a jar with source files
 */
task sourcesJar(type: Jar, dependsOn: classes, description: 'Builds the sourcesJar.', group: 'build') {
    from sourceSets.main.allSource
    classifier = 'sources'
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc, description: 'Builds the javadocJar.', group: 'build') {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

/**
 * Artifacts
 */
artifacts {
    archives sourcesJar
    archives javadocJar
}

/**
 * Deployment
 */
publishing {
    publications {
        mainProject(MavenPublication) {
            from project.components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId project.name
            version (project.version.startsWith("v")) ? project.version.substring(1) : project.version
            pom.withXml {
                def root = asNode()
                root.appendNode("description", project.description)
                root.appendNode("name", project.name)
            }
        }
    }
}

/**
 * Bintray Upload
 */
bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['mainProject']
    dryRun = false
    publish = true
    pkg {
        // jcenter repository namespace and name
        userOrg = project.name.toLowerCase()
        repo = 'maven'
        name =  project.name.toLowerCase()
        desc = project.description
        licenses = ['MIT']
        vcsUrl = System.getenv('CI_REPOSITORY_URL')
        labels = []
        publicDownloadNumbers = true
        version {
            name = String.valueOf(project.version).replace("v", "")
            vcsTag = project.version
            released = new Date()
        }
    }
}